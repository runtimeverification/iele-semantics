//  int sum(int n) {
//    int result;
//    result = 0;
//    while (n > 0) {
//      result = result + n;
//      n = n - 1;
//    }
//    return result; }


contract Sum {
  @test_result = 0x0100

  define public @sum(%n) {
    // ensure that %n is larger than or equal to 0.
    %lt = cmp lt %n, 0
    br %lt, throw

    %result = 0

  condition:
    %cond = cmp le %n, 0
    br %cond, after_loop

  loop_body:
    %result = add %result, %n
    %n      = sub %n, 1
    br condition

  after_loop:
    ret %result

  throw:
    call @iele.invalid()
  }


  define public @test_sum(%n) {
    %result = call @sum(%n)
    sstore %result, @test_result
  }
}
